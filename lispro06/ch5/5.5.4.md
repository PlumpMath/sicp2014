# 5.5.4 명령줄 한데 합치기

라벨은 퇴화된 명령줄로 볼 수 있으며 라벨은 레지스터를 요구하거나 고치지 않는다. 따라서 명령줄에 의해 레지스터가 필요하거나 변경되는 것을 결정하기 위해 다음과 같은 선택자를 쓴다.

```
(define (registers-needed s)
  (if (symbol? s) '() (car s)))
(define (registers-modified s)
  (if (symbol? s) '() (cadr s)))
(define (statements s)
  (if (symbol? s) (list s) (caddr s)))
```
그리고 주어진 명령줄이 주어진 레지스터를 요구하는지, 혹은 고치는지 알아보려고 다음 술어들을 쓴다.
```
(define (needs-register? seq reg)
  (memq reg (registers-needed seq)))
(define (modifies-register? seq reg)
  (memq reg (registers-modified seq)))
```
기본적인 조합기는 append-instruction-sequences다.
명령줄은 append-2-sequences에 의해 한 번에 두 개씩 연결된다.
집합 연산에 따라서, 필요한 레지스터의 새 집합은 합집합으로 나타낼 수 있다. 이 합집합의 요소는 seq1에서 필요한 레지스터 집합, 그리고 seq2에서 필요한 레지스터의 집합에서 seq1이 고친 레지스터 집합을 뺀 차집합이다.
```
(define (append-instruction-sequences . seqs)
  (define (append-2-sequences seq1 seq2)
    (make-instruction-sequence
     (list-union (registers-needed seq1)
                 (list-difference (registers-needed seq2)
                                  (registers-modified seq1)))
     (list-union (registers-modified seq1)
                 (registers-modified seq2))
     (append (statements seq1) (statements seq2))))
  (define (append-seq-list seqs)
    (if (null? seqs)
        (empty-instruction-sequence)
        (append-2-sequences (car seqs)
                            (append-seq-list (cdr seqs)))))
  (append-seq-list seqs))
```
이 프로시저는 리스트로 나타낸 집합을 다루기 위해 몇 가지 간단한 연산을 쓴다.
```
(define (list-union s1 s2)
  (cond ((null? s1) s2)
        ((memq (car s1) s2) (list-union (cdr s1) s2))
        (else (cons (car s1) (list-union (cdr s1) s2)))))
(define (list-difference s1 s2)
  (cond ((null? s1) '())
        ((memq (car s1) s2) (list-difference (cdr s1) s2))
        (else (cons (car s1)
                    (list-difference (cdr s1) s2)))))
```

preserving은 두번째로 중요한 조합기다. preserving은 레지스터 리스트 regs와, 명령줄 seq1과 seq2를 인자로 받아 하나의 명령줄을 결과고 돌려준다.
seq1 앞 뒤에서 seq1이 고쳤지만 seq2에서 필요한 regs 레지스터를 save와 restore 명령을 사용해 보호한다.

preserving 이 세 인자를 받아서 append를 호출한다는 것에 유념하라. 이 책에서는 append가 두 인자만 받아 처리하도록 만들어져 있지만, Scheme은 임의의 인자를 받는 append 프로시저를 표준으로 제공한다.

```
(define (preserving regs seq1 seq2)
  (if (null? regs)
      (append-instruction-sequences seq1 seq2)
      (let ((first-reg (car regs)))
        (if (and (needs-register? seq2 first-reg)
                 (modifies-register? seq1 first-reg))
            (preserving (cdr regs)
             (make-instruction-sequence
              (list-union (list first-reg)
                          (registers-needed seq1))
              (list-difference (registers-modified seq1)
                               (list first-reg))
              (append `((save ,first-reg))
                      (statements seq1)
                      `((restore ,first-reg))))
             seq2)
            (preserving (cdr regs) seq1 seq2)))))
```
또 다른 명령줄 조합기 tack-on-instruction-sequence는 compile-lambda에 의해 사용되어 프로시저 몸에 해당하는 명령줄을 다른 명령줄에 덧붙인다. 

```
(define (tack-on-instruction-sequence seq body-seq)
  (make-instruction-sequence
   (registers-needed seq)
   (registers-modified seq)
   (append (statements seq) (statements body-seq))))
```

compile-if와 compile-procedure-call에서는 parallel-instruction-sequences라는 특별한 조합기를 쓴다. 이 조합기는 검사 명령 다음으로 서로 다른 갈래의 두 명령줄을 연결한다. 이 두 명령줄은 순차적으로 실행되는 것이 아니다. 둘 중 하나만 실행된다. 이 때문에 두 번째 명령줄에서 필요한 레지스터는 비록 첫 번째 명령줄에서 고쳐졌더라도 조합한 명령줄에서 여전히 필요하다.

```
(define (parallel-instruction-sequences seq1 seq2)
  (make-instruction-sequence
   (list-union (registers-needed seq1)
               (registers-needed seq2))
   (list-union (registers-modified seq1)
               (registers-modified seq2))
   (append (statements seq1) (statements seq2))))
```
