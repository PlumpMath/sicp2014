# 5.5.3 조합 식 번역하기

## 프로시저 호출 번역
 
 <연산자를 반복한다. 타깃은 proc이고 연결은 next이다.>
 
 <피연산자를 계산(eval)하고 arg1에 피연산자의 리스트를 만든다>
 
 <지정된 타깃과 연결로 프로시저 호출을 번역한다.>
 
```scheme
(define (compile-application exp target linkage)
  (let ((proc-code (compile (operator exp) 'proc 'next))
        (operand-codes
         (map (lambda (operand) (compile operand 'val 'next))
              (operands exp))))
    (preserving '(env continue)
     proc-code
     (preserving '(proc continue)
      (construct-arglist operand-codes)
      (compile-procedure-call target linkage)))))
```

<마지막 피연산자를 번역하여 val 에 저장한다.> 
```scheme
    (assign arg1 (op list) (reg val))
```
<그 앞의 피연산자를 번역하여 val에 저장한다.>
```scheme
    (assign arg1 (op cons) (reg val) (reg arg1))
    ...
```
<마지막 피연산자를 번역하여 val에 저장한다.>
```scheme
    (assign arg1 (op cons) (reg val) (reg arg1))
```
    
피연산자가 없다면, 명령은 다음과 같다
```scheme
    (assign arg1 (const ()))
```
    
그렇지 않다면 compile-application이 제공하는 순서에 따라 피연산자 명령줄 리스트를 뒤집어야 한다.


```scheme
(define (construct-arglist operand-codes)
  (let ((operand-codes (reverse operand-codes)))
    (if (null? operand-codes)
        (make-instruction-sequence '() '(argl)
         '((assign argl (const ()))))
        (let ((code-to-get-last-arg
               (append-instruction-sequences
                (car operand-codes)
                (make-instruction-sequence '(val) '(argl)
                 '((assign argl (op list) (reg val)))))))
          (if (null? (cdr operand-codes))
              code-to-get-last-arg
              (preserving '(env)
               code-to-get-last-arg
               (code-to-get-rest-args
                (cdr operand-codes))))))))
(define (code-to-get-rest-args operand-codes)
  (let ((code-for-next-arg
         (preserving '(argl)
          (car operand-codes)
          (make-instruction-sequence '(val argl) '(argl)
           '((assign argl
              (op cons) (reg val) (reg argl)))))))
    (if (null? (cdr operand-codes))
        code-for-next-arg
        (preserving '(env)
         code-for-next-arg
         (code-to-get-rest-args (cdr operand-codes))))))
```


## 프로시저 호출하기
 
 <연산자를 반복한다. 타깃은 proc이고 연결은 next이다.>
 
 ```
  (test (op primitive-procedure?) (reg proc))
 (branch (label primitive-branch))
compiled-branch
 <번역된 프로시저를 지정된 타깃과 알맞는 연결로 적용할 코드>
primitive-branch
 (assign <target>
         (op apply-primitive-procedure)
         (reg proc)
         (reg argl))
 <linkage>
after-call
```

복합 브랜치는 기본 브랜치 다음에 끼울 라벨로 건너뛰는 연결을 사용해야 한다.

```
(define (compile-procedure-call target linkage)
  (let ((primitive-branch (make-label 'primitive-branch))
        (compiled-branch (make-label 'compiled-branch))
        (after-call (make-label 'after-call)))
    (let ((compiled-linkage
           (if (eq? linkage 'next) after-call linkage)))
      (append-instruction-sequences
       (make-instruction-sequence '(proc) '()
        `((test (op primitive-procedure?) (reg proc))
          (branch (label ,primitive-branch))))
       (parallel-instruction-sequences
        (append-instruction-sequences
         compiled-branch
         (compile-proc-appl target compiled-linkage))
        (append-instruction-sequences
         primitive-branch
         (end-with-linkage linkage
          (make-instruction-sequence '(proc argl)
                                     (list target)
           `((assign ,target
                     (op apply-primitive-procedure)
                     (reg proc)
                     (reg argl)))))))
       after-call))))
```

compile-if 에서 참 경우 브랜치와 거짓 경우 브랜치가 그랬듯이 기본 브랜치와 복합 브랜치는 원래 append-instruction-sequence를 쓰지 않고 parallel-instruction-sequence를 써서 연결한다. 이것들이 순차적으로 실행되지 않기 때문이다.

## 번역된 프로시저 적용하기

compile-lambda로 만든 프로시저처럼 번역된 프로시저에는 진입 지점이 있는데,

그것은 프로시저 코드가 시작하는 곳을 지정하는 라벨이다.

이 진입 지점에서 코드는 val에 계산 결과를 저장하고 (goto (reg continue)) 명령을 실행하면서 리턴한다.

따라서 연결이 라벨이라면 주어진 타깃과 연결로 compile-proc-appl 에 의해 만들어진 다음과 같은 번역된 프로시저 적용 코드를 기대할 수 있다.
 
 ```
  (assign continue (label proc-return))
 (assign val (op compiled-procedure-entry) (reg proc))
 (goto (reg val))
proc-return
 (assign <target> (reg val))   ; 타깃이 val이 아니라면 이 부분이 포함되어야 한다.
 (goto (label <linkage>))   ; 연결 코드
```
 
 또는 연결이 return 이라면 아래와 같다.
 
 ```
  (save continue)
 (assign continue (label proc-return))
 (assign val (op compiled-procedure-entry) (reg proc))
 (goto (reg val))
proc-return
 (assign <target> (reg val))   ; included if target is not val
 (restore continue)
 (goto (reg continue))   ; linkage code
```

이 코드는 프로시저가 라벨 proc-return 으로 돌아가 프로시저의 진입 지점으로 점프하도록 continue를 설정한다.


<연결하도록 continue 를 설정한다.>
```
(assign val (op compiled-procedure-entry) (reg proc))
(goto (reg val))
```
연결이 라벨이라면, 프로시저가 그 라벨로 돌아가도록 continue를 설정해야 한다.
```
(assign continue (label <linkage>))
(assign val (op compiled-procedure-entry) (reg proc))
(goto (reg val))
```
연결이 return 이라면 전혀 continue를 둘 필요가 없다.

(goto (reg continue)), proc-return 에서 (goto (reg continue))로 가는 것으로 프로시저가 끝난다.
```
(assign val (op compiled-procedure-entry) (reg proc))
(goto (reg val))
```
return 연결을 이렇게 구현하면, 번역기는 꼬리에서만 되돌이 하는 코드를 만든다.

이제는 return 연결과 val 타깃을 가지고 프로시저를 부르는 것을 살펴보자.

 이런 언어에서 꼬리에서만 되돌이하는 것이 어려운 까닭은 그 언어의 구현에서 프로시저 인자,
 
내부 변수뿐 아니라 리턴 주소를 저장하는 데에로 스택을 쓰기 때문이다.

이 책에서 설명하는 Scheme 구현은 메모리 재활용을 위해 메모리와 변수와 인자를 저장한다.

변수와 인자를 위해 스택을 쓰는 이유는 메모리 재활용의 필요성을 피하기 위해서다.

실제로 정교한 Lisp 번역기는 꼬리에서만 되돌이하는 일을 무너뜨리지 않고 인자를 위해 스택을 사용할 수 있다.

compile-proc-appl은 부르는 타깃이 val인지 아닌지, 연결이 return인지 아닌지 등 네 가지 경우를 고려해 위에서 살펴본 프로시저 계산 코드를 나타낸다. 명령줄이 모든 레지스터를 고칠 수 있다고 선언된다. 프로시저 실행 중에 임의의 레지스터를 고칠 수 있다고 선언된다. 프로시저 실행 중에 임의의 레지스터를 변경할 수 있기 때문이다.

```
(define (compile-proc-appl target linkage)
  (cond ((and (eq? target 'val) (not (eq? linkage 'return)))
         (make-instruction-sequence '(proc) all-regs
           `((assign continue (label ,linkage))
             (assign val (op compiled-procedure-entry)
                         (reg proc))
             (goto (reg val)))))
        ((and (not (eq? target 'val))
              (not (eq? linkage 'return)))
         (let ((proc-return (make-label 'proc-return)))
           (make-instruction-sequence '(proc) all-regs
            `((assign continue (label ,proc-return))
              (assign val (op compiled-procedure-entry)
                          (reg proc))
              (goto (reg val))
              ,proc-return
              (assign ,target (reg val))
              (goto (label ,linkage))))))
        ((and (eq? target 'val) (eq? linkage 'return))
         (make-instruction-sequence '(proc continue) all-regs
          '((assign val (op compiled-procedure-entry)
                        (reg proc))
            (goto (reg val)))))
        ((and (not (eq? target 'val)) (eq? linkage 'return))
         (error "return linkage, target not val -- COMPILE"
                target))))
```

#http://m.post.naver.com/viewer/postView.nhn?volumeNo=1721409&memberNo=12496166
